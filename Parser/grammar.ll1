Program      -> StmtList
StmtList     -> Stmt StmtListTail | ε
StmtListTail -> SEMICOLON StmtList | ε
Stmt         -> Expr | WhileStmt | ForStmt | BlockStmt | FunctionDef
FunctionDef  -> FUNCTION IDENT LPAREN ArgIdList RPAREN FunctionBody
ArgIdList    -> IDENT ArgIdListTail | ε
ArgIdListTail-> COMMA IDENT ArgIdListTail | ε
FunctionBody -> ARROW Expr SEMICOLON | BlockStmt
WhileStmt    -> WHILE Expr WhileBody
WhileBody    -> BlockStmt | Expr
ForStmt      -> FOR LPAREN IDENT IN Expr RPAREN ForBody
ForBody      -> BlockStmt | Expr
BlockStmt    -> LBRACE StmtList RBRACE
Expr         -> OrExpr | IfExpr | LetExpr
LetExpr      -> LET VarBindingList IN LetBody
LetBody      -> BlockStmt | Expr
VarBindingList -> VarBinding VarBindingListTail
VarBindingListTail -> COMMA VarBinding VarBindingListTail | ε
VarBinding   -> IDENT ASSIGN Expr
IfExpr       -> IF LPAREN Expr RPAREN IfBody ElifList ELSE IfBody
ElifList     -> ElifBranch ElifList | ε
ElifBranch   -> ELIF LPAREN Expr RPAREN IfBody
IfBody       -> BlockStmt | Expr
OrExpr       -> AndExpr OrExpr'
OrExpr'      -> OR AndExpr OrExpr' | ε
AndExpr      -> CmpExpr AndExpr'
AndExpr'     -> AND CmpExpr AndExpr' | ε
CmpExpr      -> ConcatExpr CmpExpr'
CmpExpr'     -> LESS_THAN ConcatExpr CmpExpr'
              | GREATER_THAN ConcatExpr CmpExpr'
              | LE ConcatExpr CmpExpr'
              | GE ConcatExpr CmpExpr'
              | EQ ConcatExpr CmpExpr'
              | NEQ ConcatExpr CmpExpr'
              | ε
ConcatExpr   -> AddExpr ConcatExpr'
ConcatExpr'  -> CONCAT AddExpr ConcatExpr'
              | CONCAT_WS AddExpr ConcatExpr'
              | ε
AddExpr      -> Term AddExpr'
AddExpr'     -> PLUS Term AddExpr'
              | MINUS Term AddExpr'
              | ε
Term         -> Factor Term'
Term'        -> MULT Factor Term'
              | DIV Factor Term'
              | MOD Factor Term'
              | ε
Factor       -> Power Factor'
Factor'      -> POW Power Factor'
              | ε
Power        -> Unary
Unary        -> MINUS Unary
              | Primary
Primary      -> NUMBER
              | STRING
              | TRUE
              | FALSE
              | IDENT PrimaryTail
              | LPAREN Expr RPAREN
PrimaryTail  -> LPAREN ArgList RPAREN
              | ASSIGN_DESTRUCT Expr
              | ASSIGN Expr
              | ε
ArgList      -> Expr ArgListTail
              | ε
ArgListTail  -> COMMA Expr ArgListTail
              | ε